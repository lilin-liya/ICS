#	Name:李林	ID:5130379091
#	rsum_list
		.pos 0
init:	irmovl Stack, %esp
		irmovl Stack, %ebp
		call Main
		halt

# Sample linked list
	.align 4
ele1:
		.long 0x00a
		.long ele2
ele2:
		.long 0x0b0
		.long ele3
ele3:
		.long 0xc00
		.long 0

Main:
		pushl %ebp
		rrmovl %esp, %ebp
		irmovl ele1, %eax		#start with ele1
		pushl %eax
		call rsum_list
		rrmovl %ebp, %esp
		popl %ebp
		ret

rsum_list:
		pushl %ebp
		rrmovl %esp, %ebp
		pushl %ebx
		mrmovl 8(%ebp), %ecx	#ls->val
		andl %ecx, %ecx
		jne L
		xorl %eax, %eax			#return 0;
		jmp End

L:
		mrmovl (%ecx), %ebx		#val = ls->val;
		mrmovl 4(%ecx), %eax	#ls->next
		pushl %eax
		call rsum_list			#rest = rsum_list(ls->next);
		irmovl $4, %ecx	
		addl %ecx, %esp
		addl %ebx, %eax

End:	
		popl %ebx
		popl %ebp
		ret

#	Stack
		.pos 0x100
Stack:
